#@TYPE: Machine
#@NAME: arty-zynq7
#@DESCRIPTION: Machine support for Arty-Z7 board
#

SOC_VARIANT ?= "7z"

require conf/machine/include/tune-zynq.inc
require conf/machine/include/machine-xilinx-overrides.inc
require conf/machine/include/machine-xilinx-default.inc

MACHINE_FEATURES = "ext2 vfat usbhost usbgadget"

MACHINE_ESSENTIAL_EXTRA_RDEPENDS += "device-tree"

# u-boot configuration
PREFERRED_PROVIDER_virtual/bootloader = "u-boot-digilent"
UBOOT_MACHINE = "zynq_artyz7_defconfig"
SPL_BINARY = "spl/boot.bin"

EXTRA_IMAGEDEPENDS += " \
    u-boot-zynq-uenv \
    virtual/bootloader \
    ${@ 'virtual/bitstream' if d.getVar('VIRTUAL_BITSTREAM') else ''} \
    "

WKS_FILE ?= "sdimage-bootpart.wks"

IMAGE_BOOT_FILES += " \
    boot.bin \
    u-boot.img \
    devicetree/zynq-artyz7.dtb \
    uEnv.txt \
    ${@ 'bitstream' if d.getVar('VIRTUAL_BITSTREAM') else ''} \
    "

IMAGE_FSTYPES += "wic tar.bz2"

SERIAL_CONSOLES = "115200;ttyPS0"

do_image_wic[depends] += " \
    ${MLPREFIX}u-boot-zynq-uenv:do_build \
    ${@ 'virtual/bitstream:do_build' if d.getVar('VIRTUAL_BITSTREAM') else ''} \
    "
